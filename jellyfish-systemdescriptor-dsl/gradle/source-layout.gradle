/*
 * UNCLASSIFIED
 *
 * Copyright 2020 Northrop Grumman Systems Corporation
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to use,
 * copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
 * Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 * PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 * HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 * OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */
buildscript {
    repositories {
        mavenLocal()

        maven {
            url nexusConsolidated
        }
    }

    dependencies {
        classpath "com.ngc.seaside:gradle.plugins:$seasidePluginsVersion"
    }
}

import com.ngc.seaside.gradle.util.Versions

sourceSets {
	main {
		java.srcDirs = ['src/main/java', 'src/main/xtext-gen']
		resources.srcDirs = ['src/main/resources', 'src/main/xtext-gen']
		xtendOutputDir = 'src/main/xtend-gen'
	}
	test {
		java.srcDirs = ['src/test/java', 'src/test/xtext-gen']
		resources.srcDirs = ['src/test/resources', 'src/test/xtext-gen']
		xtendOutputDir = 'src/test/xtend-gen'
	}
}

sourceSets.all {
	resources.exclude '**/*.g', '**/*.xtext', '**/*.mwe2', '**/*.xtend', '**/*._trace'
}

jar {
  from('model') {
     into('model')
  }

   manifest {
      from('src/main/resources/META-INF/MANIFEST.MF') {
         // Replace the version with the version from the gradle build.
         eachEntry {
            if(getKey().equals('Bundle-Version')) {
               setValue(Versions.makeOsgiCompliantVersion("$version"))
            }
         }
      }
   }
   
   // Include 'plugin.xml'
   from('.', { include('plugin.xml') }) 
}

plugins.withId('war') {
	webAppDirName = "WebRoot"
}

plugins.withId('org.xtext.idea-plugin') {
	assembleSandbox.metaInf.from('META-INF')
}
