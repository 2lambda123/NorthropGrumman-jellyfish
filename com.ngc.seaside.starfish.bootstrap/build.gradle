
apply plugin: 'application'

mainClassName = "${project.name}.Main"

dependencies {
    compile "org.apache.velocity:velocity:$velocityVersion"
    compile "commons-io:commons-io:$commonsIOVersion"
    compile "commons-lang:commons-lang:$commonsLangVersion"
    testCompile "junit:junit:$junitVersion"
}

// Copies all templates to distribution zip.
applicationDistribution.from('src/main/resources/templates') {
    into 'resources/config'
}

// Provide the app home directory has a system property.
applicationDefaultJvmArgs = ["-DappHome=APP_HOME_PLACEHOLDER"]

// Set the property to the value in the script (windows vs *nix).
startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('APP_HOME_PLACEHOLDER', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('APP_HOME_PLACEHOLDER', '%APP_HOME%')
    }
}

jar {
    //the application plugin requires that the script name be the project.name so this line
    //will overwrite the seaside parent plugin naming convention.
    archiveName = "${project.name}"
}
