/*
 * UNCLASSIFIED
 *
 * Copyright 2020 Northrop Grumman Systems Corporation
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of
 * this software and associated documentation files (the "Software"), to deal in
 * the Software without restriction, including without limitation the rights to use,
 * copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
 * Software, and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 * PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 * HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 * OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */
buildscript {
    ext {
        versionsFile = file('../versions.gradle')
    }
    apply from: versionsFile, to: project

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "com.ngc.seaside:gradle.plugins:$seasidePluginsVersion"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarqubePluginVersion"
        classpath "com.ngc.blocs:gradle.plugin:$blocsPluginVersion"
    }
}

apply plugin: 'com.ngc.seaside.release.root'
apply plugin: 'com.ngc.seaside.root.parent'

subprojects {
    apply plugin: 'com.ngc.seaside.parent'
    versionSettings {
        versionFile = versionsFile
    }

    seasideCi {
        m2OutputDirectory = m2directory
        dependencyInfoReportFile = dependenciesReportFile
        deploymentScriptFile = m2DeploymentScriptFile
    }

    task licenseCheckTemplate(type: com.hierynomus.gradle.license.tasks.LicenseCheck,
                              group: 'License',
                              description: 'Check for header consistency of velocity templates.') {
        source 'src/main/template', 'src/main/templates'
        include '**/*.vm'
    }

    task licenseFormatTemplate(type: com.hierynomus.gradle.license.tasks.LicenseFormat,
                               group: 'License',
                               description: 'Applies the configured license to velocity templates.') {
        source 'src/main/template', 'src/main/templates'
        include '**/*.vm'
    }

    tasks.getByName('license').dependsOn(licenseCheckTemplate)
    licenseFormat.dependsOn(licenseFormatTemplate)
}
