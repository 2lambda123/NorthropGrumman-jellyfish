apply plugin: 'com.ngc.seaside.repository'
apply plugin: 'com.ngc.seaside.ci'
apply plugin: 'com.ngc.seaside.eclipse.updatesite'
apply plugin: 'com.ngc.seaside.eclipse.p2'

eclipseDistribution {
   linuxVersion = 'eclipse-dsl-photon-R-linux-gtk-x86_64'
   windowsVersion = 'eclipse-dsl-photon-R-win32-x86_64'
   linuxDownloadUrl = "https://nexusrepomgr.ms.northgrum.com/repository/raw-ng-repo/ceacide/${linuxVersion}.zip"
   windowsDownloadUrl = "https://nexusrepomgr.ms.northgrum.com/repository/raw-ng-repo/ceacide/${windowsVersion}.zip"

   unzipEclipse.doLast {
      configurations {
         // Exclude transitive dependencies that are already bundled with eclipse
         plugin {
            pluginsDirectory.eachFileMatch({ it.endsWith '.jar' }) {
               if (!it.name.startsWith('com.google.inject')) {
                  exclude module: it.name.substring(0, it.name.indexOf('_'))
               }
            }
         }
      }
   }
}

eclipseUpdateSite {
   
   def sdFeature = feature {
      id = 'com.ngc.seaside.systemdescriptor.feature'
      label = 'JellyFish SystemDescriptor DSL'
      version = project.version
      providerName = 'Northrop Grumman Corporation'
      description {
         url = 'http://www.systemdescriptor.seaside.ngc.com/description'
         text = 'This is the JellyFish System Descriptor Domain Specific Language Eclipse plugin.'
      }
      copyright {
         url = 'http://www.systemdescriptor.seaside.ngc.com/copyright'
         text = project.resources.text.fromFile(project.file('src/main/resources/license.txt')).asString()
      }
      license {
         url = 'http://www.systemdescriptor.seaside.ngc.com/license'
         text = copyright.text
      }
      plugin {
         id = 'com.ngc.seaside.systemdescriptor'
         version = '0.0.0'
         unpack = false
      }
      plugin {
         id = 'com.ngc.seaside.systemdescriptor.ui'
         version = '0.0.0'
         unpack = false
      }
      plugin {
         id = 'com.ngc.seaside.systemdescriptor.ide'
         version = '0.0.0'
         unpack = false
      }
   }

   def sdCategory = category {
      name = 'system_descriptor_category_id'
      label = 'System Descriptor Plugin'
      description = 'Eclipse Plugin for the Seaside System Descriptor'
      feature sdFeature
   }
   
   def externalUpdateSites = [
      'https://cucumber.io/cucumber-eclipse/update-site',                 // Cucumber
      'https://nodeclipse.github.io/updates/markdown',                    // Markdown editor
      // 'https://raw.github.com/satyagraha/gfm_viewer/master/p2-composite' // Markdown git viewer - really slow
   ]

   externalUpdateSites.each {
      p2.remoteRepository(it) {
         // Include all the external site plugins
         plugins { p ->
            dependencies {
               plugin p.dependency
            }
         }
         // Include all the external site features
         features { externalFeature ->
            eclipseUpdateSite.feature externalFeature
            sdCategory.feature externalFeature
         }
      }
   }
}

dependencies {
   plugin "com.ngc.seaside:systemdescriptor:$version"
   plugin "com.ngc.seaside:systemdescriptor.ide:$version"
   plugin "com.ngc.seaside:systemdescriptor.ui:$version"

   plugin "com.ngc.seaside:systemdescriptor.scenario.impl.standardsteps:$version"
   plugin "com.ngc.seaside:jellyfish.cli.command.analyzebudget:$version"
   plugin "com.ngc.seaside:service.log.impl.common.log4jlogservice:$version"
}
