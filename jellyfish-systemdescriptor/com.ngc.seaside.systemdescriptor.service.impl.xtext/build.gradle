dependencies {
   api project(":systemdescriptor.model.api")
   api project(":systemdescriptor.service.api")

   implementation project(':systemdescriptor.model.impl.xtext')
   implementation project(':systemdescriptor.model.impl.basic')
   implementation "com.ngc.seaside:systemdescriptor:$version"

   implementation "com.google.inject:guice:$guiceVersion"
   implementation "com.google.inject.extensions:guice-multibindings:$guiceVersion"
   implementation "com.ngc.blocs:service.api:$blocsVersion"
   implementation "commons-io:commons-io:$commonsIOVersion"
   implementation "org.apache.maven:maven-model:$mavenVersion"

   testImplementation "junit:junit:$junitVersion"
   testImplementation "org.mockito:mockito-core:$mockitoVersion"
   testImplementation "com.ngc.blocs:test.impl.common.testutilities:$blocsVersion"
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava' && details.requested.version == '19.0') {
            details.useVersion '26.0-jre'
            details.because 'causes nexus lifecycle security vulnerability'
        }
    }
}

jar {
   manifest {
      // Note we don't import org.eclipse.xtext.util.  If we do, this bundle will not allow Eclipse to start if it is
      // deployed in Eclipse.
      attributes('Export-Package': 'com.ngc.seaside.systemdescriptor.service.impl.xtext.module',
                 'Import-Package': '!org.eclipse.xtext.util, *')
   }
}
