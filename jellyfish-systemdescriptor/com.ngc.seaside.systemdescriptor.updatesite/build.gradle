import com.ngc.seaside.gradle.plugins.eclipse.updatesite.SeasideEclipseUpdateSitePlugin
import com.ngc.seaside.gradle.util.Versions

import java.nio.file.Paths

apply plugin: 'com.ngc.seaside.eclipse.updatesite'

eclipseUpdateSite {
   linuxEclipseVersion = 'eclipse-dsl-oxygen-2-linux-gtk-x86_64'
   windowsEclipseVersion = 'eclipse-dsl-oxygen-2-win32-x86_64'
   linuxDownloadUrl = "https://nexusrepomgr.ms.northgrum.com/repository/raw-ng-repo/ceacide/${linuxEclipseVersion}.zip"
   windowsDownloadUrl = "https://nexusrepomgr.ms.northgrum.com/repository/raw-ng-repo/ceacide/${windowsEclipseVersion}.zip"
}

repositories {
   mavenLocal()

   maven {
      url nexusConsolidated
   }
}

configurations {
   // Only add Guava to this configuration.
   // We do this because we have to rename the JAR to match
   // the format demanded by the update site:
   // <bundleId>_<version>.jar
   guava {
      transitive = false
   }

   // Only add guice multibindings for this configuration.
   // We do this because we have to rename the JAR to match
   // the format demanded by the update site:
   // <bundleId>_<version>.jar
   multibindings {
      transitive = false
   }
}

dependencies {
   features project(path: ':systemdescriptor.feature', configuration: 'feature')

   customPlugins project(':systemdescriptor.model.impl.xtext')
   customPlugins project(':systemdescriptor.scenario.impl.standardsteps')
   customPlugins project(':systemdescriptor.service.impl.xtext')
   customPlugins project(':service.log.impl.common.log4jlogservice')

   customPlugins project(":systemdescriptor.model.api")
   customPlugins project(":systemdescriptor.service.api")
   customPlugins project(":systemdescriptor.model.impl.basic")

   customPlugins "com.ngc.seaside:systemdescriptor:$version"
   customPlugins "com.ngc.seaside:systemdescriptor.ide:$version"
   customPlugins "com.ngc.seaside:systemdescriptor.ui:$version"

   customPlugins "com.ngc.blocs:api:$blocsVersion"
   customPlugins "com.ngc.blocs:service.api:$blocsVersion"
   customPlugins "org.glassfish:javax.json:$glassfishJsonVersion"

   eclipsePlugins name: 'org.antlr.runtime_3.2.0.v201101311130'
   eclipsePlugins name: 'org.apache.commons.logging_1.1.1.v201101211721'
   eclipsePlugins name: 'org.eclipse.emf.codegen_2.12.0.v20170609-0928'
   eclipsePlugins name: 'org.eclipse.emf.common_2.13.0.v20170609-0707'
   eclipsePlugins name: 'org.eclipse.emf.mwe.utils_1.3.21.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.language.ui_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.language_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.launch_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.lib_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.runtime_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.xtend.lib_2.12.0.v20170518-0757'
   eclipsePlugins name: 'org.eclipse.xtext.builder_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.common.types.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.common.types_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.ecore_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.generator_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.ide_2.12.0.v20170518-0959'
   eclipsePlugins name: 'org.eclipse.xtext.ui.codetemplates.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui.codetemplates_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui.ecore_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui.shared_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.util_2.12.0.v20170518-0959'
   eclipsePlugins name: 'org.eclipse.xtext.xbase.lib_2.12.0.v20170518-0757'
   eclipsePlugins name: 'org.eclipse.xtext.xbase.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.xbase_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.xtext.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext_2.12.0.v20170518-0959'
   eclipsePlugins name: 'org.objectweb.asm_5.0.1.v201404251740'

   guava "com.google.guava:guava:$guavaVersion"
   multibindings "com.google.inject.extensions:guice-multibindings:$guiceVersion"
}

task copyMultibings(type: Copy) {
   from configurations.multibindings {
      rename { String name ->
         def artifacts = configurations.multibindings.resolvedConfiguration.resolvedArtifacts
         def artifact = artifacts.find { it.file.name == name }
         def osgiVersion = Versions.makeOsgiCompliantVersion("${artifact.moduleVersion.id.version}")
         // The bundle name is all messed up for multibindings.  Just hardcode the name.
         "com.google.inject.multibindings_${osgiVersion}.${artifact.extension}"
      }
   }
   into Paths.get(project.buildDir.absolutePath, "updatesite", "plugins")
}

task copyGuava(type: Copy) {
   from configurations.guava {
      rename { String name ->
         def artifacts = configurations.guava.resolvedConfiguration.resolvedArtifacts
         def artifact = artifacts.find { it.file.name == name }
         def osgiVersion = Versions.makeOsgiCompliantVersion("${artifact.moduleVersion.id.version}")
         // And guava is messed up too...  At least according to Eclipse.
         "com.google.guava_${osgiVersion}.${artifact.extension}"
      }
   }
   into Paths.get(project.buildDir.absolutePath, "updatesite", "plugins")
}

tasks.getByName(SeasideEclipseUpdateSitePlugin.ECLIPSE_CREATE_METADATA_TASK_NAME).dependsOn(copyMultibings, copyGuava)
