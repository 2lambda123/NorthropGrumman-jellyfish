import com.ngc.seaside.gradle.util.EclipsePlugins
import com.ngc.seaside.gradle.util.Versions
import org.gradle.internal.os.OperatingSystem

import java.nio.file.Paths

ext {
   buildDir = 'build'

   eclipseLinuxVersion = 'eclipse-dsl-oxygen-2-linux-gtk-x86_64'
   eclipseWindowsVersion = 'eclipse-dsl-oxygen-2-win32-x86_64'

   eclipseVersion = OperatingSystem.current().isLinux() ? "${eclipseLinuxVersion}" : "${eclipseWindowsVersion}"

   // The URL of the Eclipse Zip.  The Eclipse Zip is only downloaded if needed.
   eclipseDownloadUrl = "https://nexusrepomgr.ms.northgrum.com/repository/raw-ng-repo/ceacide/${eclipseVersion}.zip"
   // The cache location of downloaded files.
   eclipseCache = Paths.get(gradle.gradleUserHomeDir.absolutePath, "caches", "eclipse")
   // This is the location where the Eclipse download will be unzipped to.
   // Note this name may change if the download URL changes to a different version of Eclipse.
   eclipseInstallationDirectory = Paths.get(eclipseCache.toString(), eclipseVersion)
   // The local file to save the ZIP to.
   eclipseDownloadDestFile = "${eclipseInstallationDirectory}.zip"

   // This is the location of the Eclipse we use when compiling and building the
   // update site.
   eclipsePluginsDirectory = Paths.get(eclipseInstallationDirectory.toString(), "plugins")

   // This is the location of the Eclipse exe we use to create the update site
   // metadata.
   eclipseExe = "$eclipseInstallationDirectory/eclipse" + (OperatingSystem.current().isLinux() ? '' : '.exe')
}

repositories {
   mavenLocal()

   maven {
      url nexusConsolidated
   }

   flatDir {
      dirs eclipsePluginsDirectory
   }
}

configurations {
   features

   // note that plugins is reserved
   sdPlugins {
      transitive = false
   }

   eclipsePlugins {
      transitive = false
   }

   // Only add Guava to this configuration.
   // We do this because we have to rename the JAR to match
   // the format demanded by the update site:
   // <bundleId>_<version>.jar
   guava {
      transitive = false
   }

   // Only add guice multibindings for this configuration.
   // We do this because we have to rename the JAR to match
   // the format demanded by the update site:
   // <bundleId>_<version>.jar
   multibindings {
      transitive = false
   }
}

dependencies {
   features project(path: ':systemdescriptor.feature', configuration: 'feature')

   sdPlugins project(':systemdescriptor.model.impl.xtext')
   sdPlugins project(':systemdescriptor.scenario.impl.standardsteps')
   sdPlugins project(':systemdescriptor.service.impl.xtext')
   sdPlugins project(':service.log.impl.common.log4jlogservice')

   sdPlugins project(":systemdescriptor.model.api")
   sdPlugins project(":systemdescriptor.service.api")
   sdPlugins project(":systemdescriptor.model.impl.basic")

   sdPlugins "com.ngc.seaside:systemdescriptor:$version"
   sdPlugins "com.ngc.seaside:systemdescriptor.ide:$version"
   sdPlugins "com.ngc.seaside:systemdescriptor.ui:$version"

   sdPlugins "com.ngc.blocs:api:$blocsVersion"
   sdPlugins "com.ngc.blocs:service.api:$blocsVersion"
   sdPlugins "org.glassfish:javax.json:$glassfishJsonVersion"

   eclipsePlugins name: 'org.antlr.runtime_3.2.0.v201101311130'
   eclipsePlugins name: 'org.apache.commons.logging_1.1.1.v201101211721'
   eclipsePlugins name: 'org.eclipse.emf.codegen_2.12.0.v20170609-0928'
   eclipsePlugins name: 'org.eclipse.emf.common_2.13.0.v20170609-0707'
   eclipsePlugins name: 'org.eclipse.emf.mwe.utils_1.3.21.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.language.ui_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.language_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.launch_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.lib_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.emf.mwe2.runtime_2.9.1.201705291011'
   eclipsePlugins name: 'org.eclipse.xtend.lib_2.12.0.v20170518-0757'
   eclipsePlugins name: 'org.eclipse.xtext.builder_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.common.types.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.common.types_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.ecore_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.generator_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.ide_2.12.0.v20170518-0959'
   eclipsePlugins name: 'org.eclipse.xtext.ui.codetemplates.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui.codetemplates_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui.ecore_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui.shared_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.util_2.12.0.v20170518-0959'
   eclipsePlugins name: 'org.eclipse.xtext.xbase.lib_2.12.0.v20170518-0757'
   eclipsePlugins name: 'org.eclipse.xtext.xbase.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext.xbase_2.12.0.v20170519-0752'
   eclipsePlugins name: 'org.eclipse.xtext.xtext.ui_2.12.0.v20170519-0809'
   eclipsePlugins name: 'org.eclipse.xtext_2.12.0.v20170518-0959'
   eclipsePlugins name: 'org.objectweb.asm_5.0.1.v201404251740'

   guava "com.google.guava:guava:$guavaVersion"
   multibindings "com.google.inject.extensions:guice-multibindings:$guiceVersion"
}

task clean {
   doLast {
      delete("$buildDir")
   }
}

task downloadEclipse() {
   doLast {
      def destFile = file("$eclipseDownloadDestFile")
      destFile.getParentFile().mkdirs()
      println "Downloading Eclipse SDK from $eclipseDownloadUrl..."
      new URL("$eclipseDownloadUrl").withInputStream { is ->
         destFile.withOutputStream { it << is }
      }
   }
}
downloadEclipse.onlyIf { !file("$eclipseDownloadDestFile").exists() }

task unzipEclipse(type: Copy, dependsOn: downloadEclipse) {
   from zipTree(file("$eclipseDownloadDestFile"))
   into "$eclipseCache"
}
unzipEclipse.onlyIf { !file("$eclipseInstallationDirectory").exists() }

task copyFeatures(type: Copy) {
   from configurations.features
   into { "$buildDir/updatesite/features" }

   // We have to rename the feature JARs built with Gradle to make the naming
   // convention expected by Eclipse or the feature won't install correctly.
   rename { String name ->
      EclipsePlugins.makeEclipseCompliantJarFileName(name)
   }
}

task copySdPlugins(type: Copy) {
   from configurations.sdPlugins {
      rename { String name ->
         // We have to rename the feature JARs built with Gradle to make the naming
         // convention expected by Eclipse or the feature won't install correctly.
         // For these dependencies, we need to prefix the group ID to the filename.
         def artifacts = configurations.sdPlugins.resolvedConfiguration.resolvedArtifacts
         def artifact = artifacts.find { it.file.name == name }
         def osgiVersion = Versions.makeOsgiCompliantVersion("${artifact.moduleVersion.id.version}")
         "${artifact.moduleVersion.id.group}.${artifact.name}_${osgiVersion}.${artifact.extension}"
      }
   }
   into { "$buildDir/updatesite/plugins" }
}

task copyEclipsePlugins(type: Copy) {
   from configurations.eclipsePlugins
   into { "$buildDir/updatesite/plugins" }
}

task copyMultibings(type: Copy) {
   from configurations.multibindings {
      rename { String name ->
         def artifacts = configurations.multibindings.resolvedConfiguration.resolvedArtifacts
         def artifact = artifacts.find { it.file.name == name }
         def osgiVersion = Versions.makeOsgiCompliantVersion("${artifact.moduleVersion.id.version}")
         // The bundle name is all messed up for multibindings.  Just hardcode the name.
         "com.google.inject.multibindings_${osgiVersion}.${artifact.extension}"
      }
   }
   into { "$buildDir/updatesite/plugins" }
}

task copyGuava(type: Copy) {
   from configurations.guava {
      rename { String name ->
         def artifacts = configurations.guava.resolvedConfiguration.resolvedArtifacts
         def artifact = artifacts.find { it.file.name == name }
         def osgiVersion = Versions.makeOsgiCompliantVersion("${artifact.moduleVersion.id.version}")
         // And guava is messed up too...  At least according to Eclipse.
         "com.google.guava_${osgiVersion}.${artifact.extension}"
      }
   }
   into { "$buildDir/updatesite/plugins" }
}

task createMetadata(
      dependsOn: [ unzipEclipse, copyFeatures, copySdPlugins, copyEclipsePlugins, copyMultibings, copyGuava ]) {
   // Run eclipse.exe to build the update site metadata (the content.jar and artifacts.jar)
   // files.  Note that the arguments -metadataRepository and -artifactRepository want
   // "file URLs" but -source wants a plain old absolute path.
   doLast {
      project.exec {
         commandLine(
               "$eclipseExe",
               '-nosplash',
               '-application', 'org.eclipse.equinox.p2.publisher.FeaturesAndBundlesPublisher',
               '-compress',
               '-metadataRepository', file("$buildDir/updatesite/").toURI().toURL(),
               '-artifactRepository', file("$buildDir/updatesite/").toURI().toURL(),
               '-source', new File("$buildDir", "updatesite")
         )
      }
   }
}

task createZip(type: Zip, dependsOn: createMetadata) {
   from "$buildDir/updatesite"
   destinationDir = file("$buildDir")
   archiveName = "com.ngc.seaside.systemdescriptor.updatesite-${project.version}.zip"
}

task build(dependsOn: createZip)

defaultTasks = ['build']
