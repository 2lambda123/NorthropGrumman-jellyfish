import com.ngc.seaside.gradle.plugins.util.Versions

ext {
   buildDir = 'build'
}

configurations {
   feature
}

task clean() {
   doLast {
      delete("$buildDir")
   }
}

task copyAndFilterFeatureFile(type: Copy) {
   from 'src/main/resources', { include('feature.xml') }
   destinationDir = file("$buildDir/tmp")

   filter { String line ->
      if (line.trim() == 'version="${project.version}"') {
         def osgiVersion = Versions.makeOsgiCompliantVersion("${project.version}")
         line = "version=\"$osgiVersion\""
      }
      return line
   }
}

task createJar(type: Zip, dependsOn: [copyAndFilterFeatureFile]) {
   from "$buildDir/tmp"
   destinationDir = file("$buildDir")
   archiveName = "${project.group}.${project.name}-${project.version}.jar"
}

task build(dependsOn: createJar) {
}

artifacts {
   feature createJar
}

defaultTasks = ['build']
